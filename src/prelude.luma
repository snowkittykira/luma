#string: #
  [a .= b]: &= a b
  [. .? a]: &string? a

  [a .=  b]: [&=  a b]
  [a .~= b]: [&~= a b]
  [a .<  b]: [&<  a b]
  [a .>  b]: [&>  a b]
  [a .<= b]: [&<= a b]
  [a .>= b]: [&>= a b]

  [self.length         ]: &string-length self
  [self.substring i j  ]: &string-substring self i j
  [self.contains? other]: &string-contains? self other
  [self.concat    other]: &string-concat self other
  [self.utf8-char-at i ]: &utf8-char-at self i
  [self.to-number]:
    num: &string-to-number self
    or num false
  [self.to-string]: self

#bool: #
  [a .= b]: &= a b
  [. .? a]: &bool? a
  [a .to-string]: &value-to-string a

#number: #
  [. .? a]: &number? a

  [a .to-string]: &value-to-string a

  [a .+  b]: &+  a b
  [a .-  b]: &-  a b
  [a .*  b]: &*  a b
  [a ./  b]: &/  a b
  [a .=  b]: &=  a b
  [a .~= b]: &~= a b
  [a .<  b]: &<  a b
  [a .>  b]: &>  a b
  [a .<= b]: &<= a b
  [a .>= b]: &>= a b
  [a .sqrt]: &sqrt a

else: true

[not a]: if a false true

[+  a b]: a .+  b
[-  a b]: a .-  b
[*  a b]: a .*  b
[/  a b]: a ./  b
[=  a b]: a .=  b
[~= a b]: a .~= b
[<  a b]: a .<  b
[>  a b]: a .>  b
[<= a b]: a .<= b
[>= a b]: a .>= b

#collection: #
  [self.each f]: self.iterator.each f
  [self.all? f]: self.iterator.all? f

#iterator: #
  [self.map f]: # #iterator
    [. .empty?]: self.empty?
    [. .item]: f self.item
    [. .advance]: self.advance

  [self.each f]:
    while [not self.empty?]:
      f self.item
      self.advance

  [self.to-list]:
    list: [#list]
    while [not self.empty?]:
      list.push self.item
      self.advance
    list

  [self.skip n]:
    while [0 .< n]:
      set n [n .- 1]
      self.advance
    self

  [self.all? f]:
    result: true
    while [and result [not self.empty?]]:
      set result [f self.item]
      self.advance
    result

#list: # #collection
  [. .? a]: has? a #list

  [.]: # #list
    .length: 0

  [. a..]:
    list: [#list]
    [&lua-get 'assign_values_to_array'] list a..
    list

  [. .append lists..]:
    result: [#list]
    [#list lists..].each [. a]:
      result.push-items a
    result

  [self.get i]:
    &lua-get-index self i

  [self.set i v]:
    assert [0 .<= i] 'index out of range'
    assert [i .< self.length] 'index out of range'
    &lua-set-index self i v

  [self.push a]:
    &lua-set-index self self.length a
    set self.length [self.length .+ 1]

  [self.pop]:
    assert [0 .< self.length] 'cant pop empty list'
    set self.length [self.length .- 1]
    value: &lua-get-index self self.length
    &lua-remove-index self self.length
    value

  [self.peek]:
    assert [0 .< self.length] 'cant peek empty list'
    self.get [self.length .- 1]

  [self.push-items items]:
    items.each [. i]: [self.push i]

  [self.empty?]:
    self.length .= 0

  [self.iterator]: #list-iterator self

  [self.drop n]: [self.iterator.skip n].to-list

  [self.drop-last n]:
    result: [#list]
    i: 0
    end: self.length .- n
    while [i .< end]:
      result.push [self.get i]
      set i [i .+ 1]
    result

  [self.last]:
    self.get [self.length .- 1]

  [self.map f]:
    result: [#list]
    i: 0
    len: self.length
    while [i .< len]:
      result.push [f [self.get i]]
      set i [i .+ 1]
    result

  [self.concat]:
    (todo: rename to concat-strings)
    &lua-concat self

  [self.sort le-fn]:
    sorted: 1
    while [sorted .< self.length]:
      value: self.get sorted
      i: sorted
      while [and [0 .< i] [not [le-fn [self.get [i .- 1]] value]]]:
        self.set i [self.get [i .- 1]]
        set i [i .- 1]
      self.set i value
      set sorted [sorted .+ 1]

  (TODO: ideally iterator.each would just be fast enough)
  [self.each f]:
    i: 0
    end: self.length
    while [i .< end]:
      f [self.get i]
      set i [i .+ 1]

#list-iterator: # #iterator
  [. list]: # #list-iterator
    .list: list
    .index: 0
  [self.empty?]:  self.list.length .<= self.index
  [self.item]:    self.list.get self.index
  [self.advance]:
    assert [not self.empty?]
    set self.index [self.index .+ 1]

#table: #
  [. .? a]: has? a #table

  [.]: # #table
    .-index: [#]

  [. pairs..]:
    table: [#table]
    loop: #
      [.]: false
      [. pair pairs..]:
        table.set pair.key pair.value
        loop pairs..
    [loop pairs..]
    table

  [self.has? key]:
    &lua-has-index? self.-index key

  [self.set key value]:
    &lua-set-index self.-index key value

  [self.get key]:
    &lua-get-index self.-index key

  [self.get-or key default]:
    if [self.has? key]
      &lua-get-index self.-index key
      default

  [self.remove key]:
    &lua-remove-index self.-index key

  [self.key-iterator]:
    #table-key-iterator self

  [self.pair-iterator]:
    self.key-iterator.map [. key]:
      #pair key [self.get key]

#table-key-iterator: # #iterator
  [. table]: # #table-key-iterator
    .table: table
    .first?: true
    .key: false

  (todo: redesign the low level interface to make this cleaner)
  [self.empty?]: if self.first?
    not [&lua-has-next-index? self.table.-index]
    not [&lua-has-next-index? self.table.-index self.key]
  [self.item]: if self.first?
    &lua-next-index self.table.-index
    &lua-next-index self.table.-index self.key
  [self.advance]:
    assert [not self.empty?]
    set self.key self.item
    set self.first? false

#pair: #
  [. .? a]: has? a #pair

  [. key value]: # #pair
    .key: key
    .value: value

[read-file name]:
  &read-file name

[write-file name data]:
  [&write-file name data]

[quote-string str]:
  &quote-string str

[safe-identifier str]:
  &safe-identifier str

[combine-strings items..]:
  (combines lists-of-lists..-of-strings)
  [flatten-to-list list]:
    flat-list: [#list]
    [flatten-loop items]:
      items.each [. item]:
        if [#string.? item]
          flat-list.push item
          flatten-loop item
    [flatten-loop list]
    flat-list
  [flatten-to-list [#list items..]].concat

[get-args n]:
  result: [#list]
  n: 1
  while [&args n]:
    result.push [&args n]
    set n [n .+ 1]
  result

[get-time]:
  [&get-time]

command-line-args: get-args 1

#
  .#bool: #bool
  .#string: #string
  .#number: #number
  .else: else
  .not: not
  .+: +
  .-: -
  .*: *
  ./: /
  .=: =
  .~=: ~=
  .<: <
  .>: >
  .<=: <=
  .>=: >=
  .#collection: #collection
  .#list: #list
  .#table: #table
  .#pair: #pair
  .read-file: read-file
  .write-file: write-file
  .quote-string: quote-string
  .safe-identifier: safe-identifier
  .combine-strings: combine-strings
  .command-line-args: command-line-args
  .get-time: get-time
